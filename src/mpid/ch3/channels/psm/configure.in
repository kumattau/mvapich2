AC_PREREQ(2.59)
AC_INIT(configure.in)

dnl
dnl Set the directory that contains support scripts such as install-sh and
dnl config.guess
dnl
AC_CONFIG_AUX_DIR(../../../../../confdb)

dnl
dnl Definitions will be placed in this file rather than in the DEFS variable
dnl
AC_CONFIG_HEADER(include/mpidi_ch3i_psm_conf.h)
AH_TOP([/* -*- Mode: C; c-basic-offset:4 ; -*- */
/*  
 *  (C) 2001 by Argonne National Laboratory.
 *      See COPYRIGHT in top-level directory.
 */
#ifndef MPIDI_CH3I_SOCK_CONF_H_INCLUDED
#define MPIDI_CH3I_SOCK_CONF_H_INCLUDED
])
AH_BOTTOM([#endif])

echo "RUNNING CONFIGURE FOR THE PSM CHANNEL"

dnl
dnl First check that we have a clean build if we are doing a VPATH build
PAC_VPATH_CHECK()

PAC_LOAD_BASE_CACHE

AC_PROG_INSTALL
PAC_PROG_CHECK_INSTALL_WORKS
#
# On Mac OS/X, install breaks libraries unless used with the -p switch
PAC_PROG_INSTALL_BREAKS_LIBS
# We also need mkdir -p.
PAC_PROG_MKDIR_P
PAC_PROG_MAKE
AC_CHECK_PROGS(AR,ar)
if test -z "$AR" ; then
    AC_MSG_ERROR([The program ar is required for building MVAPICH2.  Make sure that your path includes ar])
fi
# Note that we set RANLIB to true if we don't find it (some systems neither
# need it nor provide it)
AC_CHECK_PROGS(RANLIB,ranlib,true)

dnl AC_CHECK_HEADER(net/if.h) fails on Solaris; extra header files needed
AC_TRY_COMPILE([
#include <sys/types.h>
#include <sys/socket.h>
#include <net/if.h>
],,lac_cv_header_net_if_h=yes,lac_cv_header_net_if_h=no)

echo "checking for net/if.h... $lac_cv_header_net_if_h"

if test "$lac_cv_header_net_if_h" = "yes" ; then
    AC_DEFINE(HAVE_NET_IF_H, 1, [Define if you have the <net/if.h> header file.])
fi

AC_CHECK_HEADERS(				\
	netdb.h					\
	sys/ioctl.h				\
	sys/socket.h				\
	sys/sockio.h				\
	sys/types.h				\
	errno.h)

# netinet/in.h often requires sys/types.h first.  With AC 2.57, check_headers
# does the right thing, which is to test whether the header is found 
# by the compiler, but this can cause problems when the header needs 
# other headers.  2.57 changes the syntax (!) of check_headers to allow 
# additional headers.
AC_CACHE_CHECK([for netinet/in.h],ac_cv_header_netinet_in_h,[
AC_TRY_COMPILE([#ifdef HAVE_SYS_TYPES_H
#include <sys/types.h>
#endif
#include <netinet/in.h>],[int a=0;],
    ac_cv_header_netinet_in_h=yes,
    ac_cv_header_netinet_in_h=no)])
if test "$ac_cv_header_netinet_in_h" = yes ; then
    AC_DEFINE(HAVE_NETINET_IN_H,1,[Define if netinet/in.h exists])
fi

CPPFLAGS="${CPPFLAGS}"

AC_SUBST(device_name)
AC_SUBST(channel_name)

AC_SUBST(AR)
AC_SUBST(AR_FLAGS)
AC_SUBST(RANLIB)
AC_SUBST(MPILIBNAME)
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
PAC_CC_SUBDIR_SHLIBS
AC_SUBST(master_top_srcdir)
# Master_top_builddir is imported from the top; use it for the "top"
# (used for things like libtool)
if test -z "$master_top_builddir" ; then
    if test -s ../../../../../.mpich2 ; then
        master_top_builddir=../../../../..
    fi
fi
AC_SUBST(master_top_builddir)
# We need the script that is used to create shared libraries if either
# we are creating shared libraries for MVAPICH2 or we are creating the
# dynamically loadable library for the debugger interface
if test -z "$CREATESHLIB" ; then
   if test ! -d ../../../../util ; then 
       mkdir ../../../../util
   fi
   CREATESHLIB=`cd ../../../../util && pwd`
   CREATESHLIB="$CREATESHLIB/createshlib"
fi
AC_SUBST(CREATESHLIB)
if test -z "$BUILD_DLLS" ; then
    BUILD_DLLS=no
fi
AC_SUBST(BUILD_DLLS)
dnl
dnl Dependency handling
AC_SUBST(MAKE_DEPEND_C)
dnl
dnl Etags
AC_SUBST(ETAGS)
AC_SUBST(ETAGSADD)
export ETAGS
export ETAGSADD

PAC_UPDATE_BASE_CACHE


AC_ARG_WITH(psm, [--with-psm=path - specify path where psm include directory and lib directory can be found],
        if test "${with_psm}" != "yes" -a "${with_psm}" != "no" ; then
            LDFLAGS="$LDFLAGS -L${with_psm}/lib64 -L${with_psm}/lib"
                CPPFLAGS="$CPPFLAGS -I${with_psm}/include"
                fi,)
AC_ARG_WITH(psm-include, [--with-psm-include=path - specify path to psm include directory],
        if test "${with_psm_include}" != "yes" -a "${with_psm_include}" != "no" ; then
            CPPFLAGS="$CPPFLAGS -I${with_psm_include}"
            fi,)
AC_ARG_WITH(psm-lib, [--with-psm-lib=path - specify path to psm lib directory],
        if test "${with_psm_lib}" != "yes" -a "${with_psm_lib}" != "no" ; then
            LDFLAGS="$LDFLAGS -L${with_psm_lib}"
            fi,)

AC_CHECK_HEADER([psm.h], , [
    AC_MSG_ERROR(['psm.h not found.  Did you specify --with-psm= or --with-psm-include=?'])
])
AC_CHECK_LIB(psm_infinipath, psm_init, , [
    AC_MSG_ERROR(['psm_infinipath library not found.  Did you specify --with-psm= or --with-psm-lib=?'])
])

# Add the MVAPICH2 include flags to CPPFLAGS.  See the discussion above about
# which include flags should start in MPICH2_INCLUDE_FLAGS and which in 
# CPPFLAGS
CPPFLAGS="$CPPFLAGS $MPICH2_INCLUDE_FLAGS"

# define the ABI version of the channel ABI that we are using.
ABIVERSION=1:1
AC_SUBST(ABIVERSION)

dnl Add a step to create the dependencies directory if it is not present
AC_OUTPUT_COMMANDS([if grep 'dependencies:' Makefile >/dev/null 2>&1 ; then
    if test ! -s src/.deps/alldeps ; then 
    echo "Updating dependency directories .deps" 
    find src -type d -print | sed -e 's%\(.*\)%(cd \1 \&\& grep MAKE_DEPEND_C Makefile >/dev/null 2>\&1 \&\& ( test -d .deps || mkdir .deps) \&\& touch .deps/alldeps)%g' | sh
fi; fi])

AC_OUTPUT(Makefile src/Makefile localdefs)
